buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: "com.android.application"

dependencies {
    implementation "androidx.core:core:1.3.2"
    implementation "com.crossbowffs.remotepreferences:remotepreferences:0.8"
    implementation "org.jsoup:jsoup:1.10.2"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.preference:preference:1.1.0"
    compileOnly "de.robv.android.xposed:api:82"
    compileOnly "de.robv.android.xposed:api:82:sources"

    implementation "androidx.work:work-runtime:2.5.0"
    implementation "androidx.concurrent:concurrent-futures:1.1.0"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        versionCode 12
        versionName "1.5.0"
        minSdkVersion 21

        /*
         * QuoteLock is not compatible with API24 and above due to the
         * behavior changes in connectivity intents. If someone wants to
         * fix this, feel free, but be aware that JobScheduler is already
         * buggy enough as it is.
         */
        targetSdkVersion 23
        buildConfigField("int", "MODULE_VERSION", "3")
        buildConfigField("int", "CUSTOM_QUOTES_DB_VERSION", "1")
        buildConfigField("int", "QUOTE_COLLECTIONS_DB_VERSION", "1")
        buildConfigField("String", "LOG_TAG", "\"QuoteLock\"")
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            res.srcDir "res"
            java.srcDir "src"
            assets.srcDir "assets"
        }
    }

    buildTypes {
        debug {
            buildConfigField("int", "LOG_LEVEL", "2")
            buildConfigField("boolean", "LOG_TO_XPOSED", "false")
        }

        release {
            buildConfigField("int", "LOG_LEVEL", "4")
            buildConfigField("boolean", "LOG_TO_XPOSED", "true")
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
